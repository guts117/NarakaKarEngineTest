#version 460 core
layout(local_size_x = 32, local_size_y = 32) in;

uniform float dt;
uniform float revert;
uniform int field_WRITE;
uniform int field_n;
uniform int field_n_hat_READ;
uniform int field_n_1_READ;
uniform int readVelocityIndex;

layout(rgba16f, binding = 0) uniform image2DArray field;
layout(binding = 1) uniform sampler2DArray velocities;

vec4 TexelFetch_2DArray_bilinear(sampler2DArray texArray, int arrayId, vec2 coord)
{
  vec2 iuv = floor(coord);
  vec2 fuv = fract(coord);

  vec4 a = texelFetch(texArray, ivec3((iuv + vec2(0.0, 0.0)), arrayId), 0);
  vec4 b = texelFetch(texArray, ivec3((iuv + vec2(1.0, 0.0)), arrayId), 0);
  vec4 c = texelFetch(texArray, ivec3((iuv + vec2(0.0, 1.0)), arrayId), 0);
  vec4 d = texelFetch(texArray, ivec3((iuv + vec2(1.0, 1.0)), arrayId), 0);

  return mix(mix(a, b, fuv.x), mix(c, d, fuv.x), fuv.y);
}

vec2 TexelFetch_2DArray_RK(sampler2DArray texArray, int arrayId, vec2 pos)
{
  vec2 v1 = TexelFetch_2DArray_bilinear(texArray, arrayId, pos).xy;
  vec2 v2 = TexelFetch_2DArray_bilinear(texArray, arrayId, pos + 0.5 * v1 * dt).xy;
  vec2 v3 = TexelFetch_2DArray_bilinear(texArray, arrayId, pos + 0.5 * v2 * dt).xy;
  vec2 v4 = TexelFetch_2DArray_bilinear(texArray, arrayId, pos + v3 * dt).xy;

  return (v1 + 2.0 * (v2 + v3) + v4) * (1.0 / 6.0);
}

vec4 ClampFieldValue_Bilinear(vec4 value, vec2 coord)
{
  vec2 npos = floor(coord);
	
  vec4 a = imageLoad(field, ivec3(npos + vec2(0.0, 0.0), field_n));
  vec4 b = imageLoad(field, ivec3(npos + vec2(1.0, 0.0), field_n));
  vec4 c = imageLoad(field, ivec3(npos + vec2(0.0, 1.0), field_n));
  vec4 d = imageLoad(field, ivec3(npos + vec2(1.0, 1.0), field_n));

  vec4 vMin = min(min(min(a, b), c), d);
  vec4 vMax = max(max(max(a, b), c), d);

  return clamp(value, vMin, vMax);
}

void main()
{
  vec2 tSize = vec2(imageSize(field));

  ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

  vec4 qAdv = imageLoad(field, ivec3(pixelCoords, field_n_1_READ));

  vec4 r = qAdv + 0.5 * imageLoad(field, ivec3(pixelCoords, field_n))
				- 0.5 * imageLoad(field, ivec3(pixelCoords, field_n_hat_READ));

  vec2 v = TexelFetch_2DArray_RK(velocities, readVelocityIndex, vec2(pixelCoords));
  vec2 pos = pixelCoords - dt * v;
  vec4 rClamped = ClampFieldValue_Bilinear(r, pos);

  r = length(rClamped - r) > revert ? qAdv : rClamped;

  imageStore(field, ivec3(pixelCoords, field_WRITE), r);
}